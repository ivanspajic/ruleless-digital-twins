@prefix : <http://www.semanticweb.org/ivans/ontologies/2025/instance-model-1#>.
@prefix meta: <http://www.semanticweb.org/ivans/ontologies/2025/ruleless-digital-twins/>.
@prefix ssn: <http://www.w3.org/ns/ssn/>.
@prefix sosa: <http://www.w3.org/ns/sosa/>.



// These rules are used as a verification mechanism against malformed or impossible Conditions. In cases of
// errors, these rules will explicitly print error messages such that the coordinator catches them and
// throws exceptions.

// Verifies that system optimizations are not conflicting.
[ruleVer1a: (?platform rdf:type sosa:Platform)
            (?platform meta:optimizesFor ?propertyChange1)
            (?propertyChange1 ssn:forProperty ?property)
            (?platform meta:optimizesFor ?propertyChange2)
            (?propertyChange2 ssn:forProperty ?property)
            (?propertyChange1 meta:affectsPropertyWith meta:ValueIncrease)
            (?propertyChange2 meta:affectsPropertyWith meta:ValueDecrease)
            ->  print("Error!")
                print(?platform, "optimizes for", ?property, "with ValueIncrease through", ?propertyChange1)
                print(?platform, "optimizes for", ?property, "with ValueDecrease through", ?propertyChange2)
]

// Marks single-constraint Conditions, such as >15, for operator checks. 
[ruleVer1b: (?condition rdf:type meta:Condition)
            (?condition rdf:type ?aNode)
            (?aNode rdf:type owl:Restriction)
            (?aNode owl:hasValue ?property)
            (?aNode owl:onProperty ?operator)
            ->  (?aNode rdf:comment ?condition)
                (?aNode rdf:comment "operator check")
                (?aNode rdf:comment "property check")
]

// Marks conjunctive Conditions, such as >15 and <22, for further checks.
[ruleVer1c: (?condition rdf:type meta:Condition)
            (?condition rdf:type ?aNode1)
            (?aNode1 owl:intersectionOf ?aList1)
            (?aList1 rdf:first ?aNode2)
            (?aNode2 rdf:type owl:Restriction)
            (?aNode2 owl:hasValue ?property1)
            (?aNode2 owl:onProperty ?operator1)
            (?aList1 rdf:rest ?aList2)
            (?aList2 rdf:first ?aNode3)
            (?aNode3 rdf:type owl:Restriction)
            (?aNode3 owl:hasValue ?property2)
            (?aNode3 owl:onProperty ?operator2)
            makeTemp(?identifier)
            ->  (?aNode2 rdf:comment ?condition)
                (?aNode2 rdf:comment ?identifier)
                (?aNode2 rdf:comment "conjunction check")
                (?aNode2 rdf:comment "operator check")
                (?aNode2 rdf:comment "property check")
                (?aNode3 rdf:comment ?condition)
                (?aNode3 rdf:comment ?identifier)
                (?aNode3 rdf:comment "conjunction check")
                (?aNode3 rdf:comment "operator check")
                (?aNode3 rdf:comment "property check")
]

// Marks single-single-constraint disjunctive Conditions, such as <15 or >22, for further checks.
[ruleVer1d: (?condition rdf:type meta:Condition)
            (?condition rdf:type ?aNode1)
            (?aNode1 owl:unionOf ?aList1)
            (?aList1 rdf:first ?aNode2)
            (?aNode2 rdf:type owl:Restriction)
            (?aNode2 owl:hasValue ?property1)
            (?aNode2 owl:onProperty ?operator1)
            (?aList1 rdf:rest ?aList2)
            (?aList2 rdf:first ?aNode3)
            (?aNode3 rdf:type owl:Restriction)
            (?aNode3 owl:hasValue ?property2)
            (?aNode3 owl:onProperty ?operator2)
            ->  (?aNode2 rdf:comment ?condition)
                (?aNode2 rdf:comment "operator check")
                (?aNode2 rdf:comment "property check")
                (?aNode3 rdf:comment ?condition)
                (?aNode3 rdf:comment "operator check")
                (?aNode3 rdf:comment "property check")
]

// Marks double-single-constraint disjunctive Conditions, such as (>15 and <22) or >34, for further checks.
[ruleVer1e: (?condition rdf:type meta:Condition)
            (?condition rdf:type ?aNode1)
            (?aNode1 owl:unionOf ?aList1)
            (?aList1 rdf:first ?aNode2)
            (?aNode2 owl:intersectionOf ?aList2)
            (?aList2 rdf:first ?aNode3)
            (?aNode3 rdf:type owl:Restriction)
            (?aNode3 owl:hasValue ?property1)
            (?aNode3 owl:onProperty ?operator1)
            (?aList2 rdf:rest ?aList3)
            (?aList3 rdf:first ?aNode4)
            (?aNode4 rdf:type owl:Restriction)
            (?aNode4 owl:hasValue ?property2)
            (?aNode4 owl:onProperty ?operator2)
            (?aList1 rdf:rest ?aList4)
            (?aList4 rdf:first ?aNode5)
            (?aNode5 rdf:type owl:Restriction)
            (?aNode5 owl:hasValue ?property3)
            (?aNode5 owl:onProperty ?operator3)
            makeTemp(?identifier)
            ->  (?aNode3 rdf:comment ?condition)
                (?aNode3 rdf:comment ?identifier)
                (?aNode3 rdf:comment "conjunction check")
                (?aNode3 rdf:comment "operator check")
                (?aNode3 rdf:comment "property check")
                (?aNode4 rdf:comment ?condition)
                (?aNode4 rdf:comment ?identifier)
                (?aNode4 rdf:comment "conjunction check")
                (?aNode4 rdf:comment "operator check")
                (?aNode4 rdf:comment "property check")
                (?aNode5 rdf:comment ?condition)
                (?aNode5 rdf:comment "operator check")
                (?aNode5 rdf:comment "property check")
]

// Marks double-double-constraint disjunctive Conditions such as (>15 and <22) or (>34 and <45), for
// further checks.
[ruleVer1f: (?condition rdf:type meta:Condition)
            (?condition rdf:type ?aNode1)
            (?aNode1 owl:unionOf ?aList1)
            (?aList1 rdf:first ?aNode2)
            (?aNode2 owl:intersectionOf ?aList2)
            (?aList2 rdf:first ?aNode3)
            (?aNode3 rdf:type owl:Restriction)
            (?aNode3 owl:hasValue ?property1)
            (?aNode3 owl:onProperty ?operator1)
            (?aList2 rdf:rest ?aList3)
            (?aList3 rdf:first ?aNode4)
            (?aNode4 rdf:type owl:Restriction)
            (?aNode4 owl:hasValue ?property2)
            (?aNode4 owl:onProperty ?operator2)
            (?aList1 rdf:rest ?aList4)
            (?aList4 rdf:first ?aNode5)
            (?aNode5 owl:intersectionOf ?aList5)
            (?aList5 rdf:first ?aNode6)
            (?aNode6 rdf:type owl:Restriction)
            (?aNode6 owl:hasValue ?property3)
            (?aNode6 owl:onProperty ?operator3)
            (?aList5 rdf:rest ?aList6)
            (?aList6 rdf:first ?aNode7)
            (?aNode7 rdf:type owl:Restriction)
            (?aNode7 owl:hasValue ?property4)
            (?aNode7 owl:onProperty ?operator4)
            makeTemp(?identifier1)
            makeTemp(?identifier2)
            ->  (?aNode3 rdf:comment ?condition)
                (?aNode3 rdf:comment ?identifier1)
                (?aNode3 rdf:comment "conjunction check")
                (?aNode3 rdf:comment "operator check")
                (?aNode3 rdf:comment "property check")
                (?aNode4 rdf:comment ?condition)
                (?aNode4 rdf:comment ?identifier1)
                (?aNode4 rdf:comment "conjunction check")
                (?aNode4 rdf:comment "operator check")
                (?aNode4 rdf:comment "property check")
                (?aNode6 rdf:comment ?condition)
                (?aNode6 rdf:comment ?identifier2)
                (?aNode6 rdf:comment "conjunction check")
                (?aNode6 rdf:comment "operator check")
                (?aNode6 rdf:comment "property check")
                (?aNode7 rdf:comment ?condition)
                (?aNode7 rdf:comment ?identifier2)
                (?aNode7 rdf:comment "conjunction check")
                (?aNode7 rdf:comment "operator check")
                (?aNode7 rdf:comment "property check")
]

// Prints errors if constraints use disallowed properties as operators.
[ruleVer2a: (?aNode rdf:comment "operator check")
            (?aNode rdf:comment ?condition)
            (?condition rdf:type meta:Condition)
            (?aNode owl:onProperty ?operator)
            notEqual(?operator, meta:equalTo)
            notEqual(?operator, meta:greaterThan)
            notEqual(?operator, meta:greaterThanOrEqualTo)
            notEqual(?operator, meta:lessThan)
            notEqual(?operator, meta:lessThanOrEqualTo)
            ->  print("Error!")
                print("Disallowed operator", ?operator, "used in Condition", ?condition)
]

// Prints errors if constraints use disallowed classes in place of Properties.
[ruleVer2b: (?aNode rdf:comment "property check")
            (?aNode rdf:comment ?condition)
            (?condition rdf:type meta:Condition)
            (?aNode owl:hasValue ?property)
            notEqual(?property, "property check")
            notEqual(?property, "operator check")
            notEqual(?property, "conjunction check")
            notEqual(?property, ?condition)
            noValue(?property rdf:type ssn:Property)
            noValue(?property rdf:type sosa:ObservableProperty)
            noValue(?property rdf:type meta:ConfigurableParameter)
            ->  print("Error!")
                print("Disallowed property class", ?property, "used in Condition", ?condition)
]

// Prints errors if Properties in Conditions have unsupported value types.
[ruleVer2c: (?aNode rdf:comment "property check")
            (?aNode rdf:comment ?condition)
            (?condition rdf:type meta:Condition)
            (?aNode owl:hasValue ?property)
            (?property meta:hasValue ?value)
            notEqual(?property, "property check")
            notEqual(?property, "operator check")
            notEqual(?property, "conjunction check")
            notEqual(?property, ?condition)
            notDType(?value, xsd:double)
            notDType(?value, xsd:int)
            notDType(?value, xsd:dateTimeStamp)
            ->  print("Error!")
                print("Unsupported Property value type", ?value, "used in Condition", ?condition)
]

// These rules print errors in cases of Conditions that are always impossible.
[ruleVer2d: (?aNode1 rdf:comment "conjunction check")
            (?aNode2 rdf:comment "conjunction check")
            (?aNode1 rdf:comment ?condition1)
            (?condition1 rdf:type meta:Condition)
            (?condition1 ssn:forProperty ?property)
            (?aNode1 rdf:comment ?identifier1)
            (?aNode1 owl:hasValue ?property1)
            (?aNode1 owl:onProperty ?operator1)
            (?aNode2 rdf:comment ?condition2)
            (?aNode2 rdf:comment ?identifier2)
            (?aNode2 owl:hasValue ?property2)
            (?aNode2 owl:onProperty ?operator2)
            isBNode(?identifier1)
            notEqual(?condition1, "property check")
            notEqual(?condition1, "operator check")
            notEqual(?condition1, "conjunction check")
            notEqual(?condition1, ?identifier1)
            notEqual(?condition1, ?identifier2)
            equal(?condition1, ?condition2)
            equal(?identifier1, ?identifier2)
            equal(?property1, ?property2)
            equal(?operator1, meta:equalTo)
            equal(?operator2, meta:lessThan)
            ->  print("Error!")
                print("Constraint in Condition", ?condition1, "requires that", ?property, "be both")
                print(?operator1, "and", ?operator2, ?property1)
]

[ruleVer2e: (?aNode1 rdf:comment "conjunction check")
            (?aNode2 rdf:comment "conjunction check")
            (?aNode1 rdf:comment ?condition1)
            (?condition1 rdf:type meta:Condition)
            (?condition1 ssn:forProperty ?property)
            (?aNode1 rdf:comment ?identifier1)
            (?aNode1 owl:hasValue ?property1)
            (?aNode1 owl:onProperty ?operator1)
            (?aNode2 rdf:comment ?condition2)
            (?aNode2 rdf:comment ?identifier2)
            (?aNode2 owl:hasValue ?property2)
            (?aNode2 owl:onProperty ?operator2)
            isBNode(?identifier1)
            notEqual(?condition1, "property check")
            notEqual(?condition1, "operator check")
            notEqual(?condition1, "conjunction check")
            notEqual(?condition1, ?identifier1)
            notEqual(?condition1, ?identifier2)
            equal(?condition1, ?condition2)
            equal(?identifier1, ?identifier2)
            equal(?property1, ?property2)
            equal(?operator1, meta:equalTo)
            equal(?operator2, meta:greaterThan)
            ->  print("Error!")
                print("Constraint in Condition", ?condition1, "requires that", ?property, "be both")
                print(?operator1, "and", ?operator2, ?property1)
]

[ruleVer2f: (?aNode1 rdf:comment "conjunction check")
            (?aNode2 rdf:comment "conjunction check")
            (?aNode1 rdf:comment ?condition1)
            (?condition1 rdf:type meta:Condition)
            (?condition1 ssn:forProperty ?property)
            (?aNode1 rdf:comment ?identifier1)
            (?aNode1 owl:hasValue ?property1)
            (?aNode1 owl:onProperty ?operator1)
            (?aNode2 rdf:comment ?condition2)
            (?aNode2 rdf:comment ?identifier2)
            (?aNode2 owl:hasValue ?property2)
            (?aNode2 owl:onProperty ?operator2)
            isBNode(?identifier1)
            notEqual(?condition1, "property check")
            notEqual(?condition1, "operator check")
            notEqual(?condition1, "conjunction check")
            notEqual(?condition1, ?identifier1)
            notEqual(?condition1, ?identifier2)
            equal(?condition1, ?condition2)
            equal(?identifier1, ?identifier2)
            equal(?property1, ?property2)
            equal(?operator1, meta:lessThan)
            equal(?operator2, meta:greaterThan)
            ->  print("Error!")
                print("Constraint in Condition", ?condition1, "requires that", ?property, "be both")
                print(?operator1, "and", ?operator2, ?property1)
]

// These rules print errors in cases of Conditions that are always true (tautologies).
[ruleVer2g: (?aNode1 rdf:comment "conjunction check")
            (?aNode2 rdf:comment "conjunction check")
            (?aNode1 rdf:comment ?condition1)
            (?condition1 rdf:type meta:Condition)
            (?condition1 ssn:forProperty ?property)
            (?aNode1 rdf:comment ?identifier1)
            (?aNode1 owl:hasValue ?property1)
            (?aNode1 owl:onProperty ?operator1)
            (?aNode2 rdf:comment ?condition2)
            (?aNode2 rdf:comment ?identifier2)
            (?aNode2 owl:hasValue ?property2)
            (?aNode2 owl:onProperty ?operator2)
            isBNode(?identifier1)
            notEqual(?condition1, "property check")
            notEqual(?condition1, "operator check")
            notEqual(?condition1, "conjunction check")
            notEqual(?condition1, ?identifier1)
            notEqual(?condition1, ?identifier2)
            equal(?condition1, ?condition2)
            equal(?identifier1, ?identifier2)
            equal(?property1, ?property2)
            equal(?operator1, meta:lessThan)
            equal(?operator2, meta:greaterThanOrEqualTo)
            ->  print("Error!")
                print(?property, "in Condition", ?condition1, "will always be", ?operator1)
                print("and", ?operator2, ?property1)
]

[ruleVer2h: (?aNode1 rdf:comment "conjunction check")
            (?aNode2 rdf:comment "conjunction check")
            (?aNode1 rdf:comment ?condition1)
            (?condition1 rdf:type meta:Condition)
            (?condition1 ssn:forProperty ?property)
            (?aNode1 rdf:comment ?identifier1)
            (?aNode1 owl:hasValue ?property1)
            (?aNode1 owl:onProperty ?operator1)
            (?aNode2 rdf:comment ?condition2)
            (?aNode2 rdf:comment ?identifier2)
            (?aNode2 owl:hasValue ?property2)
            (?aNode2 owl:onProperty ?operator2)
            isBNode(?identifier1)
            notEqual(?condition1, "property check")
            notEqual(?condition1, "operator check")
            notEqual(?condition1, "conjunction check")
            notEqual(?condition1, ?identifier1)
            notEqual(?condition1, ?identifier2)
            equal(?condition1, ?condition2)
            equal(?identifier1, ?identifier2)
            equal(?property1, ?property2)
            equal(?operator1, meta:greaterThan)
            equal(?operator2, meta:lessThanOrEqualTo)
            ->  print("Error!")
                print(?property, "in Condition", ?condition1, "will always be", ?operator1)
                print("and", ?operator2, ?property1)
]

// TODO: handle disjunctive tautologies (e.g., less than property OR greater than or equal to same property is always true)