@prefix : <http://www.semanticweb.org/ivans/ontologies/2025/instance-model-1#>.
@prefix meta: <http://www.semanticweb.org/ivans/ontologies/2025/ruleless-digital-twins/>.
@prefix ssn: <http://www.w3.org/ns/ssn/>.
@prefix sosa: <http://www.w3.org/ns/sosa/>.



// These rules are used as a verification mechanism against malformed or impossible Conditions. In cases of errors, these rules will
// explicitly print error messages such that the coordinator catches them and throws exceptions.

// Verifies that system optimizations are not conflicting.
[ruleVer1a: (?platform rdf:type sosa:Platform)
            (?platform meta:optimizesFor ?propertyChange1)
            (?propertyChange1 ssn:forProperty ?property)
            (?platform meta:optimizesFor ?propertyChange2)
            (?propertyChange2 ssn:forProperty ?property)
            (?propertyChange1 meta:affectsPropertyWith meta:ValueIncrease)
            (?propertyChange2 meta:affectsPropertyWith meta:ValueDecrease)
            ->  print("Error!")
                print(?platform, "optimizes for", ?property, "with ValueIncrease through", ?propertyChange1)
                print(?platform, "optimizes for", ?property, "with ValueDecrease through", ?propertyChange2)
]

// Marks single-constraint Conditions, such as >15, for operator checks.
[ruleVer1b: (?condition rdf:type meta:Condition)
            (?condition rdf:type ?aNode)
            (?condition ssn:forProperty ?conditionProperty)
            (?aNode rdf:type owl:Restriction)
            (?aNode owl:hasValue ?boundProperty)
            (?aNode owl:onProperty ?operator)
            ->  (?aNode :withCondition ?condition)
                (?aNode :withConditionProperty ?conditionProperty)
                (?aNode :withOperatorCheck ?operator)
                (?aNode :withPropertyCheck ?conditionProperty)
                (?aNode :withPropertyCheck ?boundProperty)
]

// Marks conjunctive Conditions, such as >15 and <22, for further checks.
[ruleVer1c: (?condition rdf:type meta:Condition)
            (?condition rdf:type ?aNode1)
            (?condition ssn:forProperty ?conditionProperty)
            (?aNode1 owl:intersectionOf ?aList1)
            (?aList1 rdf:first ?aNode2)
            (?aNode2 rdf:type owl:Restriction)
            (?aNode2 owl:hasValue ?boundProperty1)
            (?aNode2 owl:onProperty ?operator1)
            (?aList1 rdf:rest ?aList2)
            (?aList2 rdf:first ?aNode3)
            (?aNode3 rdf:type owl:Restriction)
            (?aNode3 owl:hasValue ?boundProperty2)
            (?aNode3 owl:onProperty ?operator2)
            makeTemp(?identifier)
            ->  (?aNode2 :withCondition ?condition)
                (?aNode2 :withIdentifier ?identifier)
                (?aNode2 :withConditionProperty ?conditionProperty)
                (?aNode2 :withCheck "conjunction")
                (?aNode2 :withOperatorCheck ?operator1)
                (?aNode2 :withPropertyCheck ?conditionProperty)
                (?aNode2 :withPropertyCheck ?boundProperty1)
                (?aNode3 :withCondition ?condition)
                (?aNode3 :withIdentifier ?identifier)
                (?aNode3 :withConditionProperty ?conditionProperty)
                (?aNode3 :withCheck "conjunction")
                (?aNode3 :withOperatorCheck ?operator2)
                (?aNode3 :withPropertyCheck ?boundProperty2)
]

// Marks single-single-constraint disjunctive Conditions, such as <15 or >22, for further checks.
[ruleVer1d: (?condition rdf:type meta:Condition)
            (?condition rdf:type ?aNode1)
            (?condition ssn:forProperty ?conditionProperty)
            (?aNode1 owl:unionOf ?aList1)
            (?aList1 rdf:first ?aNode2)
            (?aNode2 rdf:type owl:Restriction)
            (?aNode2 owl:hasValue ?boundProperty1)
            (?aNode2 owl:onProperty ?operator1)
            (?aList1 rdf:rest ?aList2)
            (?aList2 rdf:first ?aNode3)
            (?aNode3 rdf:type owl:Restriction)
            (?aNode3 owl:hasValue ?boundProperty2)
            (?aNode3 owl:onProperty ?operator2)
            ->  (?aNode2 :withCondition ?condition)
                (?aNode2 :withConditionProperty ?conditionProperty)
                (?aNode2 :withCheck "disjunction")
                (?aNode2 :withOperatorCheck ?operator1)
                (?aNode2 :withPropertyCheck ?conditionProperty)
                (?aNode2 :withPropertyCheck ?boundProperty1)
                (?aNode3 :withCondition ?condition)
                (?aNode3 :withConditionProperty ?conditionProperty)
                (?aNode3 :withCheck "disjunction")
                (?aNode3 :withOperatorCheck ?operator2)
                (?aNode3 :withPropertyCheck ?boundProperty2)
]

// Marks double-single-constraint disjunctive Conditions, such as (>15 and <22) or >34, for further checks.
[ruleVer1e: (?condition rdf:type meta:Condition)
            (?condition rdf:type ?aNode1)
            (?condition ssn:forProperty ?conditionProperty)
            (?aNode1 owl:unionOf ?aList1)
            (?aList1 rdf:first ?aNode2)
            (?aNode2 owl:intersectionOf ?aList2)
            (?aList2 rdf:first ?aNode3)
            (?aNode3 rdf:type owl:Restriction)
            (?aNode3 owl:hasValue ?boundProperty1)
            (?aNode3 owl:onProperty ?operator1)
            (?aList2 rdf:rest ?aList3)
            (?aList3 rdf:first ?aNode4)
            (?aNode4 rdf:type owl:Restriction)
            (?aNode4 owl:hasValue ?boundProperty2)
            (?aNode4 owl:onProperty ?operator2)
            (?aList1 rdf:rest ?aList4)
            (?aList4 rdf:first ?aNode5)
            (?aNode5 rdf:type owl:Restriction)
            (?aNode5 owl:hasValue ?boundProperty3)
            (?aNode5 owl:onProperty ?operator3)
            makeTemp(?identifier)
            ->  (?aNode3 :withCondition ?condition)
                (?aNode3 :withIdentifier ?identifier)
                (?aNode3 :withConditionProperty ?conditionProperty)
                (?aNode3 :withCheck "conjunction")
                (?aNode3 :withOperatorCheck ?operator1)
                (?aNode3 :withPropertyCheck ?conditionProperty)
                (?aNode3 :withPropertyCheck ?boundProperty1)
                (?aNode4 :withCondition ?condition)
                (?aNode4 :withIdentifier ?identifier)
                (?aNode4 :withConditionProperty ?conditionProperty)
                (?aNode4 :withCheck "conjunction")
                (?aNode4 :withOperatorCheck ?operator2)
                (?aNode4 :withPropertyCheck ?boundProperty2)
                (?aNode5 :withCondition ?condition)
                (?aNode5 :withConditionProperty ?conditionProperty)
                (?aNode5 :withOperatorCheck ?operator3)
                (?aNode5 :withPropertyCheck ?boundProperty3)
]

// Marks double-double-constraint disjunctive Conditions such as (>15 and <22) or (>34 and <45), for further checks.
[ruleVer1f: (?condition rdf:type meta:Condition)
            (?condition rdf:type ?aNode1)
            (?condition ssn:forProperty ?conditionProperty)
            (?aNode1 owl:unionOf ?aList1)
            (?aList1 rdf:first ?aNode2)
            (?aNode2 owl:intersectionOf ?aList2)
            (?aList2 rdf:first ?aNode3)
            (?aNode3 rdf:type owl:Restriction)
            (?aNode3 owl:hasValue ?boundProperty1)
            (?aNode3 owl:onProperty ?operator1)
            (?aList2 rdf:rest ?aList3)
            (?aList3 rdf:first ?aNode4)
            (?aNode4 rdf:type owl:Restriction)
            (?aNode4 owl:hasValue ?boundProperty2)
            (?aNode4 owl:onProperty ?operator2)
            (?aList1 rdf:rest ?aList4)
            (?aList4 rdf:first ?aNode5)
            (?aNode5 owl:intersectionOf ?aList5)
            (?aList5 rdf:first ?aNode6)
            (?aNode6 rdf:type owl:Restriction)
            (?aNode6 owl:hasValue ?boundProperty3)
            (?aNode6 owl:onProperty ?operator3)
            (?aList5 rdf:rest ?aList6)
            (?aList6 rdf:first ?aNode7)
            (?aNode7 rdf:type owl:Restriction)
            (?aNode7 owl:hasValue ?boundProperty4)
            (?aNode7 owl:onProperty ?operator4)
            makeTemp(?identifier1)
            makeTemp(?identifier2)
            ->  (?aNode3 :withCondition ?condition)
                (?aNode3 :withIdentifier ?identifier1)
                (?aNode3 :withConditionProperty ?conditionProperty)
                (?aNode3 :withCheck "conjunction")
                (?aNode3 :withOperatorCheck ?operator1)
                (?aNode3 :withPropertyCheck ?conditionProperty)
                (?aNode3 :withPropertyCheck ?boundProperty1)
                (?aNode4 :withCondition ?condition)
                (?aNode4 :withIdentifier ?identifier1)
                (?aNode4 :withConditionProperty ?conditionProperty)
                (?aNode4 :withCheck "conjunction")
                (?aNode4 :withOperatorCheck ?operator2)
                (?aNode4 :withPropertyCheck ?boundProperty2)
                (?aNode6 :withCondition ?condition)
                (?aNode6 :withIdentifier ?identifier2)
                (?aNode6 :withConditionProperty ?conditionProperty)
                (?aNode6 :withCheck "conjunction")
                (?aNode6 :withOperatorCheck ?operator3)
                (?aNode6 :withPropertyCheck ?boundProperty3)
                (?aNode7 :withCondition ?condition)
                (?aNode7 :withIdentifier ?identifier2)
                (?aNode7 :withConditionProperty ?conditionProperty)
                (?aNode7 :withCheck "conjunction")
                (?aNode7 :withOperatorCheck ?operator4)
                (?aNode7 :withPropertyCheck ?boundProperty4)
]



// Prints errors if constraints use disallowed object properties as operators.
[ruleVer2a: (?aNode :withOperatorCheck ?operator)
            (?condition rdf:type ?aNode)
            notEqual(?operator, meta:equalTo)
            notEqual(?operator, meta:greaterThan)
            notEqual(?operator, meta:greaterThanOrEqualTo)
            notEqual(?operator, meta:lessThan)
            notEqual(?operator, meta:lessThanOrEqualTo)
            ->  print("Error!")
                print("Disallowed operator", ?operator, "used in Condition", ?condition)
]

// Prints errors if Conditions use anything else in place of Properties.
[ruleVer2b: (?aNode :withPropertyCheck ?property)
            (?condition rdf:type ?aNode)
            noValue(?property rdf:type ssn:Property)
            noValue(?property rdf:type sosa:ObservableProperty)
            noValue(?property rdf:type meta:ConfigurableParameter)
            ->  print("Error!")
                print("Disallowed Thing", ?property, "used in place of a Property in Condition", ?condition)
]

// Prints errors if Properties in Conditions have unsupported value types.
[ruleVer2d: (?aNode :withPropertyCheck ?property)
            (?condition rdf:type ?aNode)
            (?property meta:hasValue ?value)
            notDType(?value, xsd:double)
            notDType(?value, xsd:int)
            notDType(?value, xsd:dateTimeStamp)
            ->  print("Error!")
                print("Unsupported Property value type", ?value, "used in Condition", ?condition)
]



// The following rules print errors in cases of Conditions that are always impossible.

// = and < the same property is impossible.
[ruleVer2e: (?aNode1 :withCheck "conjunction")
            (?aNode2 :withCheck "conjunction")
            (?aNode1 :withCondition ?condition1)
            (?condition1 rdf:type meta:Condition)
            (?condition1 ssn:forProperty ?conditionProperty)
            (?aNode1 :withIdentifier ?identifier1)
            (?aNode1 owl:hasValue ?boundProperty1)
            (?aNode1 owl:onProperty meta:equalTo)
            (?aNode2 :withCondition ?condition2)
            (?aNode2 :withIdentifier ?identifier2)
            (?aNode2 owl:hasValue ?boundProperty2)
            (?aNode2 owl:onProperty meta:lessThan)
            isBNode(?identifier1)
            equal(?condition1, ?condition2)
            equal(?identifier1, ?identifier2)
            equal(?boundProperty1, ?boundProperty2)
            ->  print("Error!")
                print("Constraint in Condition", ?condition1, "requires that", ?conditionProperty,)
                print("be both equal to and less than", ?boundProperty1)
]

// = and > the same property is impossible.
[ruleVer2f: (?aNode1 :withCheck "conjunction")
            (?aNode2 :withCheck "conjunction")
            (?aNode1 :withCondition ?condition1)
            (?condition1 rdf:type meta:Condition)
            (?condition1 ssn:forProperty ?conditionProperty)
            (?aNode1 :withIdentifier ?identifier1)
            (?aNode1 owl:hasValue ?boundProperty1)
            (?aNode1 owl:onProperty meta:equalTo)
            (?aNode2 :withCondition ?condition2)
            (?aNode2 :withIdentifier ?identifier2)
            (?aNode2 owl:hasValue ?boundProperty2)
            (?aNode2 owl:onProperty meta:greaterThan)
            isBNode(?identifier1)
            equal(?condition1, ?condition2)
            equal(?identifier1, ?identifier2)
            equal(?boundProperty1, ?boundProperty2)
            ->  print("Error!")
                print("Constraint in Condition", ?condition1, "requires that", ?conditionProperty)
                print("be both equal to and greater than", ?boundProperty1)
]

// < and > the same property is impossible.
[ruleVer2g: (?aNode1 :withCheck "conjunction")
            (?aNode2 :withCheck "conjunction")
            (?aNode1 :withCondition ?condition1)
            (?condition1 rdf:type meta:Condition)
            (?condition1 ssn:forProperty ?conditionProperty)
            (?aNode1 :withIdentifier ?identifier1)
            (?aNode1 owl:hasValue ?boundProperty1)
            (?aNode1 owl:onProperty meta:lessThan)
            (?aNode2 :withCondition ?condition2)
            (?aNode2 :withIdentifier ?identifier2)
            (?aNode2 owl:hasValue ?boundProperty2)
            (?aNode2 owl:onProperty meta:greaterThan)
            isBNode(?identifier1)
            equal(?condition1, ?condition2)
            equal(?identifier1, ?identifier2)
            equal(?boundProperty1, ?boundProperty2)
            ->  print("Error!")
                print("Constraint in Condition", ?condition1, "requires that", ?conditionProperty)
                print("be both less than and greater than" ?boundProperty1)
]

// < and >= the same property is impossible.
[ruleVer2h: (?aNode1 :withCheck "conjunction")
            (?aNode2 :withCheck "conjunction")
            (?aNode1 :withCondition ?condition1)
            (?condition1 rdf:type meta:Condition)
            (?condition1 ssn:forProperty ?conditionProperty)
            (?aNode1 :withIdentifier ?identifier1)
            (?aNode1 owl:hasValue ?boundProperty1)
            (?aNode1 owl:onProperty meta:lessThan)
            (?aNode2 :withCondition ?condition2)
            (?aNode2 :withIdentifier ?identifier2)
            (?aNode2 owl:hasValue ?boundProperty2)
            (?aNode2 owl:onProperty meta:greaterThanOrEqualTo)
            isBNode(?identifier1)
            equal(?condition1, ?condition2)
            equal(?identifier1, ?identifier2)
            equal(?boundProperty1, ?boundProperty2)
            ->  print("Error!")
                print("Constraint in Condition", ?condition1, "requires that", ?conditionProperty)
                print("be both less than and greater than or equal to", ?boundProperty1)
]

// > and <= the same property is impossible.
[ruleVer2i: (?aNode1 :withCheck "conjunction")
            (?aNode2 :withCheck "conjunction")
            (?aNode1 :withCondition ?condition1)
            (?condition1 rdf:type meta:Condition)
            (?condition1 ssn:forProperty ?conditionProperty)
            (?aNode1 :withIdentifier ?identifier1)
            (?aNode1 owl:hasValue ?boundProperty1)
            (?aNode1 owl:onProperty meta:greaterThan)
            (?aNode2 :withCondition ?condition2)
            (?aNode2 :withIdentifier ?identifier2)
            (?aNode2 owl:hasValue ?boundProperty2)
            (?aNode2 owl:onProperty meta:lessThanOrEqualTo)
            isBNode(?identifier1)
            equal(?condition1, ?condition2)
            equal(?identifier1, ?identifier2)
            equal(?boundProperty1, ?boundProperty2)
            ->  print("Error!")
                print("Constraint in Condition", ?condition1, "requires that", ?conditionProperty)
                print("be both greater than and less than or equal to", ?boundProperty1)
]



// These rules print errors in cases of Conditions that are always true (tautologies).

// Condition Properties should not also be constraint bounds since that will, depending on the operator, always
// either be true (a tautology) or impossible (e.g., roomTemperature < roomTemperature).
[ruleVer2k: (?aNode :withCondition ?condition)
            (?condition ssn:forProperty ?conditionProperty)
            (?aNode owl:hasValue ?boundProperty)
            equal(?conditionProperty, ?boundProperty)
            ->  print("Error!")
                print("Condition", ?condition, "uses", ?conditionProperty)
                print("as both the Condition Property and constraint bound.")
]

// > or <= the same property is always true.
[ruleVer2k: (?aNode1 :withCheck "disjunction")
            (?aNode2 :withCheck "disjunction")
            (?aNode1 :withCondition ?condition1)
            (?condition1 rdf:type meta:Condition)
            (?condition1 ssn:forProperty ?conditionProperty)
            (?aNode1 owl:hasValue ?boundProperty1)
            (?aNode1 owl:onProperty meta:greaterThan)
            (?aNode2 :withCondition ?condition2)
            (?aNode2 owl:hasValue ?boundProperty2)
            (?aNode2 owl:onProperty meta:lessThanOrEqualTo)
            equal(?condition1, ?condition2)
            equal(?boundProperty1, ?boundProperty2)
            ->  print("Error!")
                print(?conditionProperty, "in Condition", ?condition1)
                print("will always be greater than or less than or equal to", ?boundProperty1)
]

// < or >= the same property is always true.
[ruleVer2l: (?aNode1 :withCheck "disjunction")
            (?aNode2 :withCheck "disjunction")
            (?aNode1 :withCondition ?condition1)
            (?condition1 rdf:type meta:Condition)
            (?condition1 ssn:forProperty ?conditionProperty)
            (?aNode1 owl:hasValue ?boundProperty1)
            (?aNode1 owl:onProperty meta:lessThan)
            (?aNode2 :withCondition ?condition2)
            (?aNode2 owl:hasValue ?boundProperty2)
            (?aNode2 owl:onProperty meta:greaterThanOrEqualTo)
            equal(?condition1, ?condition2)
            equal(?boundProperty1, ?boundProperty2)
            ->  print("Error!")
                print(?conditionProperty, "in Condition", ?condition1)
                print("will always be less than or greater than or equal to", ?boundProperty1)
]

// >= or <= the same property is always true.
[ruleVer2m: (?aNode1 :withCheck "disjunction")
            (?aNode2 :withCheck "disjunction")
            (?aNode1 :withCondition ?condition1)
            (?condition1 rdf:type meta:Condition)
            (?condition1 ssn:forProperty ?conditionProperty)
            (?aNode1 owl:hasValue ?boundProperty1)
            (?aNode1 owl:onProperty meta:lessThanOrEqualTo)
            (?aNode2 :withCondition ?condition2)
            (?aNode2 owl:hasValue ?boundProperty2)
            (?aNode2 owl:onProperty meta:greaterThanOrEqualTo)
            equal(?condition1, ?condition2)
            equal(?boundProperty1, ?boundProperty2)
            ->  print("Error!")
                print(?conditionProperty, "in Condition", ?condition1)
                print("will always be less than or equal to or greater than or equal to", ?boundProperty1)
]



// These rules print errors in cases of multiple Conditions with conflicting constraints, making them impossible
// to be satisfied at the same time.

// If Condition1 states some Property must be less than a Property bound, and Condition2 states that Property must
// be greater than that same Property bound, then these are conflicting Conditions.
[ruleVer2n: (?aNode1 :withCondition ?condition1)
            (?condition1 rdf:type meta:Condition)
            (?condition1 ssn:forProperty ?conditionProperty1)
            (?aNode1 owl:hasValue ?boundProperty1)
            (?aNode1 owl:onProperty meta:lessThan)
            (?aNode2 :withCondition ?condition2)
            (?condition2 rdf:type meta:Condition)
            (?condition2 ssn:forProperty ?conditionProperty2)
            (?aNode2 owl:hasValue ?boundProperty2)
            (?aNode2 owl:onProperty meta:greaterThan)
            notEqual(?condition1, ?condition2)
            equal(?conditionProperty1, ?conditionProperty2)
            equal(?boundProperty1, ?boundProperty2)
            ->  print("Error!")
                print("Conditions", ?condition1, "and", ?condition2, "define conflicting constraints.")
                print(?conditionProperty1, "must be both less than and greater than", ?boundProperty1)
]

// If Condition1 states some Property must be less than a Property bound, and Condition2 states that Property must
// be greater than or equal to that same Property bound, then these are conflicting Conditions.
[ruleVer2o: (?aNode1 :withCondition ?condition1)
            (?condition1 rdf:type meta:Condition)
            (?condition1 ssn:forProperty ?conditionProperty1)
            (?aNode1 owl:hasValue ?boundProperty1)
            (?aNode1 owl:onProperty meta:lessThan)
            (?aNode2 :withCondition ?condition2)
            (?condition2 rdf:type meta:Condition)
            (?condition2 ssn:forProperty ?conditionProperty2)
            (?aNode2 owl:hasValue ?boundProperty2)
            (?aNode2 owl:onProperty meta:greaterThanOrEqualTo)
            notEqual(?condition1, ?condition2)
            equal(?conditionProperty1, ?conditionProperty2)
            equal(?boundProperty1, ?boundProperty2)
            ->  print("Error!")
                print("Conditions", ?condition1, "and", ?condition2, "define conflicting constraints.")
                print(?conditionProperty1, "must be both less than and greater than or equal to", ?boundProperty1)
]

// If Condition1 states some Property must be less than a Property bound, and Condition2 states that Property must
// be equal to that same Property bound, then these are conflicting Conditions.
[ruleVer2p: (?aNode1 :withCondition ?condition1)
            (?condition1 rdf:type meta:Condition)
            (?condition1 ssn:forProperty ?conditionProperty1)
            (?aNode1 owl:hasValue ?boundProperty1)
            (?aNode1 owl:onProperty meta:lessThan)
            (?aNode2 :withCondition ?condition2)
            (?condition2 rdf:type meta:Condition)
            (?condition2 ssn:forProperty ?conditionProperty2)
            (?aNode2 owl:hasValue ?boundProperty2)
            (?aNode2 owl:onProperty meta:equalTo)
            notEqual(?condition1, ?condition2)
            equal(?conditionProperty1, ?conditionProperty2)
            equal(?boundProperty1, ?boundProperty2)
            ->  print("Error!")
                print("Conditions", ?condition1, "and", ?condition2, "define conflicting constraints.")
                print(?conditionProperty1, "must be both less than and equal to", ?boundProperty1)
]

// If Condition1 states some Property must be greater than a Property bound, and Condition2 states that Property must
// be equal to that same Property bound, then these are conflicting Conditions.
[ruleVer2q: (?aNode1 :withCondition ?condition1)
            (?condition1 rdf:type meta:Condition)
            (?condition1 ssn:forProperty ?conditionProperty1)
            (?aNode1 owl:hasValue ?boundProperty1)
            (?aNode1 owl:onProperty meta:greaterThan)
            (?aNode2 :withCondition ?condition2)
            (?condition2 rdf:type meta:Condition)
            (?condition2 ssn:forProperty ?conditionProperty2)
            (?aNode2 owl:hasValue ?boundProperty2)
            (?aNode2 owl:onProperty meta:equalTo)
            notEqual(?condition1, ?condition2)
            equal(?conditionProperty1, ?conditionProperty2)
            equal(?boundProperty1, ?boundProperty2)
            ->  print("Error!")
                print("Conditions", ?condition1, "and", ?condition2, "define conflicting constraints.")
                print(?conditionProperty1, "must be both greater than and equal to", ?boundProperty1)
]

// If Condition1 states some Property must be greater than a Property bound, and Condition2 states that Property must
// be less than or equal to that same Property bound, then these are conflicting Conditions.
[ruleVer2r: (?aNode1 :withCondition ?condition1)
            (?condition1 rdf:type meta:Condition)
            (?condition1 ssn:forProperty ?conditionProperty1)
            (?aNode1 owl:hasValue ?boundProperty1)
            (?aNode1 owl:onProperty meta:greaterThan)
            (?aNode2 :withCondition ?condition2)
            (?condition2 rdf:type meta:Condition)
            (?condition2 ssn:forProperty ?conditionProperty2)
            (?aNode2 owl:hasValue ?boundProperty2)
            (?aNode2 owl:onProperty meta:lessThanOrEqualTo)
            notEqual(?condition1, ?condition2)
            equal(?conditionProperty1, ?conditionProperty2)
            equal(?boundProperty1, ?boundProperty2)
            ->  print("Error!")
                print("Conditions", ?condition1, "and", ?condition2, "define conflicting constraints.")
                print(?conditionProperty1, "must be both greater than and less than or equal to", ?boundProperty1)
]